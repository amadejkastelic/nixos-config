{
  lib,
  python3,
  nix-update-script,
  fetchFromGitHub,
  meson,
  ninja,
  pkg-config,
  qt6,
}:

python3.pkgs.buildPythonApplication (finalAttrs: {
  pname = "leshade";
  version = "2.5.0";
  __structuredAttrs = true;

  pyproject = false;

  src = fetchFromGitHub {
    owner = "Ishidawg";
    repo = "LeShade";
    tag = finalAttrs.version;
    hash = "sha256-QhfXs003/70zMWzYp1pZXICYHlt0GQnwCx4QgDDlsik=";
  };

  dontWrapQtApps = true;

  nativeBuildInputs = [
    meson
    ninja
    pkg-config
    qt6.wrapQtAppsHook
  ];

  buildInputs = [
    qt6.qtbase
    qt6.qtwayland
  ];

  propagatedBuildInputs = with python3.pkgs; [
    pyside6
    certifi
  ];

  makeWrapperArgs = [
    "--prefix"
    "PYTHONPATH"
    ":"
    (placeholder "out" + "/share/leshade")
  ];

  preFixup = ''
    makeWrapperArgs+=("''${qtWrapperArgs[@]}")
  '';

  postInstall = ''
    rm $out/bin/leshade
    cp $out/share/leshade/main.py $out/bin/leshade
    chmod +x $out/bin/leshade

    substituteInPlace $out/share/applications/leshade.desktop \
      --replace-fail "Icon=io.github.ishidawg.LeShade" "Icon=leshade"
  '';

  passthru.updateScript = nix-update-script {
    extraArgs = [ "--flake" ];
  };

  meta = {
    description = "ReShade manager for Linux";
    mainProgram = "leshade";
    homepage = "https://github.com/Ishidawg/LeShade";
    changelog = "https://github.com/Ishidawg/LeShade/releases/tag/${finalAttrs.version}";
    license = lib.licenses.mit;
  };
})
